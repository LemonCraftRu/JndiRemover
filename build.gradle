buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "spongepowered"
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

group 'ru.icosider'
version = "$mc_version-$mod_version"

targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8
compileJava*.options*.encoding = 'UTF-8'

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'
}

mixin {
    add sourceSets.main, "mixins.jndi_remover.refmap.json"
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

repositories {
    mavenCentral()
    maven {
        name = "Spongepowered"
        url = "https://repo.spongepowered.org/maven"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'
    shade('org.spongepowered:mixin:0.8.1-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'commons-codec'
        exclude module: 'asm-commons'
    }
    if (System.getProperty("idea.sync.active") != "true")
        annotationProcessor 'org.spongepowered:mixin:0.8.1:processor'
}

jar {
    destinationDir = file("$rootDir/compiled")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    exclude "net/minecraft/**"

    manifest {
        attributes([
                "Specification-Title": "jndiremover",
                "Specification-Vendor": "jndi_remover",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" : "jndi_remover",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "FMLCorePluginContainsFMLMod": true,
                "MixinConfigs": "mixins.jndi_remover.json",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": 0
        ])
    }
    configurations.shade.each { dep -> from(project.zipTree(dep)) }
}

jar.finalizedBy('reobfJar')